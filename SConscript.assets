import os, fnmatch

env = Environment()
Import('rglob')

ASSET_DIR = 'public/baseq3/'

ConvToOGG = Builder(
	action = 'oggenc $SOURCE -o $TARGET',
	suffix = '.ogg',
	src_suffix = '.wav')
env['BUILDERS']['ConvToOGG'] = ConvToOGG

ConvToDXT = Builder(
	action = 'crunch -file $SOURCE -rescalemode nearest -fileformat dds -outsamedir',
	suffix = '.dds')
env['BUILDERS']['ConvToDXT'] = ConvToDXT

ConvToPNG = Builder(
	action = 'convert $SOURCE $TARGET',
	suffix = '.png',
	src_suffix = '.jpg')
env['BUILDERS']['ConvToPNG'] = ConvToPNG

#
# Audio
#
audio_dirs = ['music', 'sound']
audio_src = []
for dir in audio_dirs:
	audio_src.extend(rglob(ASSET_DIR + dir, '*.wav'))
env.Alias('audio', [env.ConvToOGG(x) for x in audio_src])

#
# Textures
#
texture_dirs = ['env', 'gfx/colors', 'gfx/damage', 'gfx/misc', 'models', 'textures']
texture_src = []
for dir in texture_dirs:
	texture_src.extend(rglob(ASSET_DIR + dir, '*.jpg'))
	texture_src.extend(rglob(ASSET_DIR + dir, '*.tga'))
env.Alias('textures', [env.ConvToDXT(x) for x in texture_src])

#
# Images
#
image_dirs = ['gfx/2d', 'icons', 'levelshots']
image_src = []
for dir in image_dirs:
	image_src.extend(rglob(ASSET_DIR + dir, '*.jpg'))
	image_src.extend(rglob(ASSET_DIR + dir, '*.tga'))
env.Alias('images', [env.ConvToPNG(x) for x in image_src])